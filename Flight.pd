//*******************************************************
//* File:    /Flight.pd
//* Author:  UP696771
//* License: See /LICENSE
//* For full list of assumptions, please see /assumptions.txt
//*******************************************************
import 
	"Aircraft.pd",
	"Airport.pd",
	"Passenger.pd",
	"Pilot.pd";
	
class Flight ^=
abstract
	var 
		code : string,
		date : nat,
		aircraft : Aircraft,
		route: pair of (Airport, Airport), // Origin / Destination
		//origin : Airport,
		//destination : Airport,
		passengers : set of Passenger,
		pilots : set of Pilot,
		distance : nat;
	
	invariant
		#code = 8,
		#pilots > 1,// ASSUMPTION: Standard real life operating procedure dictates at least two pilots on a plane (More depending on airline and flight duration)
		route.x ~= route.y, // ASSUMPTION: No circular trips
		#passengers <= aircraft.passengerLimit, // ASSUMPTION: Flight cannot be overbooked (Occasionally policy for major airlines to compensate for average 10% no-shows)
		forall p :: pilots :- aircraft in p.flyables; // All Pilots need to be able to be able to fly the current type of aircraft
		//NOTE: No need to check uniqueness of passengers as equality is based on all properties and passengers only contain one, therefore already handled by set
interface
	schema !addPassenger(passenger : Passenger)
		pre 
			passenger ~in passengers,
			~self.isFull
		post passengers! = passengers.append(passenger);
		
	//Specification lists no required functionality for removing a passenger
	
	function 
		date,
		code,
		route,
		distance;
	
	function isFull:bool
		^= #passengers = aircraft.passengerLimit;
		
	total operator ~~(other)
		^= date~~other.date;
		
	build{!code:string, !date:nat, !aircraft:Aircraft, !route:pair of (Airport,Airport), !pilots:set of Pilot, !distance:nat}
		pre
			#code = 8,
			#pilots > 1,
			route.x ~= route.y,
			forall p :: pilots :- aircraft in p.flyables
		post passengers! = set of Passenger{};
end;