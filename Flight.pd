//*******************************************************
//* File:    /Flight.pd
//* Author:  UP696771
//* License: See /License.txt
//* Assumptions: See /Assumptions.txt
//*******************************************************
class Flight ^=
abstract
	var
		code : string,
		date : Date,
		aircraft : Aircraft,
		route : Route,
		pilot: Pilot || void,
		passengers : set of Passenger,
		distance: nat;
		
	invariant
		#code = 8, // ASSUMPTION: "8 character flight code" means any string character is valid
		route.x ~= route.y, // No circular routes
		(
			[pilot ~= null]: aircraft in (pilot is Pilot).flyables,
			[]: true
		),
		~(exists x::passengers, y::passengers :- x~=y & x.uid = y.uid), // ASSUMPTION: Airline only allows people to buy one ticket/flight
		#passengers <= aircraft.capacity;
		
confined
	build{!code:string, !date:Date, !distance:nat, !aircraft:Aircraft, !pilot:Pilot||void, !route:Route, !passengers:set of Passenger}
		pre
			#code = 8,
			route.x ~= route.y,
			~(exists x::passengers, y::passengers :- x~=y & x.uid = y.uid);
interface

	schema! addPassenger(passenger:Passenger)
		pre 
			~(exists x::passengers :- x~=passenger & x.uid = passenger.uid),
			~isFull
		post passengers! = passengers.append(passenger)
		assert passenger in passengers';

	schema! setPilot(newPilot:Pilot || void)
		pre (
			[newPilot ~= null]: aircraft in (newPilot is Pilot).flyables,
			[]: true
		)
		post pilot! = newPilot
		assert pilot' = newPilot;

	function
		code,
		date,
		aircraft,
		route,
		pilot,
		distance,
		passengers;
		
	function numOfPassengers:nat
		^= #passengers;
	
	function isFull:bool
		^= #passengers = aircraft.capacity;
		
	operator ~~ (other)
		^= date ~~ other.date;
		
	build{!code:string, !date:Date, !distance: nat, !aircraft:Aircraft, !pilot:Pilot||void, !route:Route}
		pre
			#code = 8,
			route.x ~= route.y
		post passengers! = set of Passenger{};
end;